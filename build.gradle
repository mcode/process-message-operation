plugins {
    id 'java-library'
    id 'checkstyle'
    id 'maven-publish'
    id 'signing'
}

// stick to java 8
sourceCompatibility = '1.8'
targetCompatibility = '1.8'


repositories {
    // Use jcenter for resolving dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    // mavenCentral()
    // maven {
    //  url "https://oss.sonatype.org/content/repositories/snapshots"
    // }
}

checkstyle {
  toolVersion '8.4'
  //showViolations = true
}

dependencies {
	// dependencies list is intentionally kept minimal for this
	// ie, we don't want to include the whole FHIR-JPA-Starter app
	
    // 'api' dependencies are exported to consumers, that is to say found on their compile classpath.
    api 'ca.uhn.hapi.fhir:hapi-fhir-base:4.1.0'
    api 'ca.uhn.hapi.fhir:hapi-fhir-client:4.1.0'
    api 'ca.uhn.hapi.fhir:hapi-fhir-structures-r4:4.1.0'
    
    api 'javax.servlet:javax.servlet-api:3.1.0'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.13'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

def mavenGroup = 'org.mitre.hapifhir'
def mavenVersion = '0.0.1'

publishing {
    publications {
        processMessageOperation(MavenPublication) {
            groupId mavenGroup
            version mavenVersion
            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }
            pom {
                name = 'HAPI $process-message Operation'
                description = '$process-message operation for HAPI FHIR'
                url = 'https://github.com/mcode/process-message-operation'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/mcode/process-message-operation.git'
                    developerConnection = 'scm:git:ssh://github.com:mcode/process-message-operation.git'
                    url = 'http://github.com/mcode/process-message-operation/tree/master'
                }
                developers {
                    developer {
                        name = 'Dylan Hall'
                        email = 'dehall@mitre.org'
                        organization = 'The MITRE Corporation'
                        organizationUrl = 'http://www.mitre.org/'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def snapshotUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
            def releaseUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            url mavenVersion.endsWith('SNAPSHOT') ? snapshotUrl : releaseUrl
            credentials {
                def user = project.hasProperty('ossrhUsername') ? ossrhUsername : ''
                def pw = project.hasProperty('ossrhPassword') ? ossrhPassword : ''
                username user
                password pw 
            }
        }
    }
}

signing {
  required { project.hasProperty('signing.keyId') }
  sign publishing.publications.processMessageOperation
}
